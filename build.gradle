plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '3.4.0' apply false
    id 'io.spring.dependency-management' version '1.1.6' apply false
}

allprojects {
    group = 'com.zipsoon'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        // Common
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.postgresql:postgresql'
        implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.4'

        // PostGIS
        implementation 'net.postgis:postgis-jdbc:2.5.1'
        implementation 'org.locationtech.jts:jts-core:1.19.0'

        // Utils
        implementation 'org.apache.commons:commons-lang3:3.17.0'
        implementation 'me.paulschwarz:spring-dotenv:4.0.0'

        // Lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        // Test Dependencies
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.testcontainers:testcontainers'
        testImplementation 'org.testcontainers:junit-jupiter'
        testImplementation 'org.testcontainers:postgresql'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    if (project.name == 'common') {
        tasks.named('jar') {
            enabled = false
        }

        tasks.named('bootJar') {
            enabled = false
        }
    }
}

test {
    testLogging {
        events "FAILED", "SKIPPED", "PASSED"
        exceptionFormat "full"
    }
}